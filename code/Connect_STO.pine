// ===LICENSE ===

// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// Â© Azullian

// === VERSION ===

//@version=5

// === DECLARATIONS ===

indicator('Stochastic / Connectable [Azullian]', shorttitle='Stochastic / Connectable [Azullian]', overlay=true)

// === IMPORTS ===

import azullian/azLibConnector/2 as azconnector

// === CONSTANTS ===

// â€”â€”â€”â€”â€” Constants: Timeframe options
var TF00                        ="default"
var TF01                        ="1S"
var TF02                        ="5S"
var TF03                        ="15S"
var TF04                        ="30S"
var TF05                        ="1"
var TF06                        ="3"
var TF07                        ="5"
var TF08                        ="15"
var TF09                        ="30"
var TF10                        ="45"
var TF11                        ="60"
var TF12                        ="120"
var TF13                        ="180"
var TF14                        ="240"
var TF15                        ="1D"
var TF16                        ="5D"
var TF17                        ="1W"
var TF18                        ="1M"
var TF19                        ="3M"
var TF20                        ="12M"

// â€”â€”â€”â€”â€” Constants: Signal Mode options
var ALL                         = 'All'
var ENTRIES_EXITS               = 'Entries & Exits'
var ENTRIES                     = 'Entries Only'
var EXITS                       = 'Exits Only'
var ZONE                        = 'Zone'

// â€”â€”â€”â€”â€” Constants: Signal Count options
var EVERY                       = "Every"
var SINGLE                      = "Single"
var THS                         = "Single+Threshold"
var THE                         = "Every+Threshold"

// â€”â€”â€”â€”â€” Constants: Signal Category Threshold options
var THRESHOLD                   = "Threshold"
var NO_THRESHOLD                = "No Threshold"

// â€”â€”â€”â€”â€” Constants: Connector Flow options
var BOTH                        = "Both"
var INDICATOR                   = "Indicator Only"
var CONNECTOR                   = "Connector Only"

// â€”â€”â€”â€”â€” Constants: Input Labels and Tips - Global settings
var LBL_INDICATOR_ENERGY        = "ðŸ—²ï¸"
var LBL_INDICATOR_BRIGHTNESS    = "â˜¼"
var TIP_ENERGY_BRIGHTNESS       = "Energy % / Brightness %"

var LBL_CONNECTOR               = "â†’"
var LBL_CONNECTOR_MODE          = "âŒ¥"
var LBL_THRESHOLD_TYPE          = "áƒ„"
var TIP_INPUT_FLOW_THRESH       = "Input / Flow / Threshold type"

var LBL_SIGNALS_MULTI           = "â¥…"
var LBL_SIGNALS_THRESH          = "â¥‡"
var LBL_SIGNALS_LIMIT           = "â¥±"
var TIP_MULTI_THRESH_LIMIT      = "Signals Multiplier / Threshold / Limiter"

// â€”â€”â€”â€”â€” Constants: Input Labels and Tips -  Weight system
var LBL_SIGNAL_MODE             = 'SM'
var LBL_SIGNAL_PERIOD           = 'SP'
var LBL_SIGNAL_COUNT            = 'SC'
var TIP_SIGNAL_MODE             = 'SM: Signal Mode / SP: Signal Period. # of historical bars to lookup the condition. / SC: Signal Count. Define how to count signals within Signal Period. Single: Count a single condition in period. Every: Count every condition in period. Threshold: Count a single condition if number of conditions in period equal or exceed threshold.'

var LBL_EL                      = "EL"
var LBL_XL                      = "XL"
var LBL_ES                      = "ES"
var LBL_XS                      = "XS"

var LBL_LONG_COLOR              = "ðŸ¡‘"
var LBL_SHORT_COLOR             = "ðŸ¡“"
var LBL_NEUTRAL_COLOR           = "â†•"

// â€”â€”â€”â€”â€” Constants: Input Labels and Tips -  Indicator
var LBL_ENABLE                  = 'Enable Indicator'
var TIP_STO                     = 'STO: Stochastic Oscillator: Used to predict price turning points by comparing the closing price to its price range.'

var LBL_SYMBOL                  = 'S'
var LBL_TIMEFRAME               = 'T'
var TIP_SYMBOL_TIMEFRAME        = "S: Symbol / T: Timeframe"

var LBL_LENGTH                  = 'KL'
var LBL_OVERBOUGHT              = 'OB'
var LBL_OVERSOLD                = 'OS'
var LBL_PERCK                   = '%K'
var LBL_PERCD                   = '%D'
var TIP_OB_OS                   = 'KL: %K Length / OB: Overbought / OS: Oversold'
var TIP_PK_PD                   = '%K: %K smoothing, %D: %D smoothing'

// â€”â€”â€”â€”â€” Constants: Defaults - Global settings
var ENERGY_DEFAULT              = 10
var ENERGY_MIN                  = 0
var ENERGY_MAX                  = 100
var ENERGY_STEP                 = 10
var BRIGHTNESS_DEFAULT          = 85
var BRIGHTNESS_MIN              = 0
var BRIGHTNESS_MAX              = 100 
var BRIGHTNESS_STEP             = 5
var MULTIPLIER_DEFAULT          = 1
var THRESHOLD_DEFAULT           = 12
var THRESHOLD_MIN               = 0
var THRESHOLD_MAX               = 499
var LIMITER_DEFAULT             = 499
var LIMITER_MIN                 = 0
var LIMITER_MAX                 = 499
var CONNECTOR_MODE_DEFAULT      = INDICATOR
var SIGNALS_THRES_TYPE_DEFAULT  = NO_THRESHOLD

// â€”â€”â€”â€”â€” Constants: Defaults - Indicator settings
var ENABLE_DEFAULT              = true
var SYMBOL_DEFAULT              = 'CRYPTOCAP:TOTAL'
var TIMEFRAME_DEFAULT           = timeframe.period

// â€”â€”â€”â€”â€” Constants: Defaults - Signal weight settings
var SIGNAL_MIN                  = -12
var SIGNAL_MAX                  = 12

var SIGNAL_PERIODS_DEFAULT      = 0
var SIGNAL_MODE_DEFAULT         = ENTRIES
var SIGNAL_COUNT_DEFAULT        = SINGLE

var EL_DEFAULT                  = 6
var XL_DEFAULT                  = 6
var ES_DEFAULT                  = 6
var XS_DEFAULT                  = 6

// â€”â€”â€”â€”â€” Constants: Colors
var LONG_COLOR                  = color.rgb(123, 31, 162)
var SHORT_COLOR                 = color.rgb(123, 31, 162)
var STO_COLOR                   = color.rgb(123, 31, 162)

// â€”â€”â€”â€”â€” Constants: Icons
var CAT_ICON                    = shape.diamond
var STO_ICON                    = CAT_ICON


// === INPUTS ===

// â€”â€”â€”â€”â€” Inputs: Visualisation settings
var settingsGroup = "SETTINGS"
i_indicatorEnergy = input.int(title=LBL_INDICATOR_ENERGY, defval=ENERGY_DEFAULT, minval=ENERGY_MIN, maxval=ENERGY_MAX, step=ENERGY_STEP, inline=LBL_INDICATOR_ENERGY, group=settingsGroup) / 100
i_indicatorBrightness = input.int(title=LBL_INDICATOR_BRIGHTNESS, defval=BRIGHTNESS_DEFAULT, minval=BRIGHTNESS_MIN, maxval=BRIGHTNESS_MAX, step=BRIGHTNESS_STEP, inline=LBL_INDICATOR_ENERGY, group=settingsGroup, tooltip=TIP_ENERGY_BRIGHTNESS) / 100

// â€”â€”â€”â€”â€” Inputs: Connector Settings
i_connector = input.source(title=LBL_CONNECTOR, defval=close, inline=LBL_CONNECTOR, group=settingsGroup)
i_connectorMode = input.string(title=LBL_CONNECTOR_MODE, defval=CONNECTOR_MODE_DEFAULT, options=[BOTH, INDICATOR, CONNECTOR], inline=LBL_CONNECTOR, group=settingsGroup)
i_signalsThresholdType = input.string(title=LBL_THRESHOLD_TYPE, defval=SIGNALS_THRES_TYPE_DEFAULT, options=[NO_THRESHOLD, THRESHOLD], inline=LBL_CONNECTOR, group=settingsGroup, tooltip=TIP_INPUT_FLOW_THRESH)

// â€”â€”â€”â€”â€” Inputs: Category Settings
i_signalsMultiplier = input.int(title=LBL_SIGNALS_MULTI, defval=MULTIPLIER_DEFAULT, minval=SIGNAL_MIN, maxval=SIGNAL_MAX, inline=LBL_SIGNALS_MULTI, group=settingsGroup)
i_signalsThreshold = input.int(title=LBL_SIGNALS_THRESH, defval=THRESHOLD_DEFAULT, minval=THRESHOLD_MIN, maxval=THRESHOLD_MAX, inline=LBL_SIGNALS_MULTI, group=settingsGroup)
i_signalsLimiter = input.int(title=LBL_SIGNALS_LIMIT, defval=LIMITER_DEFAULT, minval=LIMITER_MIN, maxval=LIMITER_MAX, inline=LBL_SIGNALS_MULTI, group=settingsGroup, tooltip=TIP_MULTI_THRESH_LIMIT)

// â€”â€”â€”â€”â€” Inputs: Indicator inputs
var stoGroupNameSettings = 'STO - Stochastic oscillator'
i_stoEnabled = input.bool(title=LBL_ENABLE, defval=ENABLE_DEFAULT, group=stoGroupNameSettings, tooltip=TIP_STO)

i_stoSymEnabled = input.bool(title=LBL_SYMBOL, defval=false, group=stoGroupNameSettings, inline=LBL_SYMBOL)
i_stoSymbol = input.symbol(SYMBOL_DEFAULT, title='', inline=LBL_SYMBOL, group=stoGroupNameSettings)
i_stoTimeFrameTmp = input.timeframe(defval=TF00, title=LBL_TIMEFRAME, options=[TF00,TF01,TF02,TF03,TF04,TF05,TF06,TF07,TF08,TF09,TF10,TF11,TF12,TF13,TF14,TF15,TF16,TF17,TF18,TF19,TF20], tooltip=TIP_SYMBOL_TIMEFRAME, inline=LBL_SYMBOL, group=stoGroupNameSettings) 
i_stoTimeFrame = i_stoTimeFrameTmp == TF00 ? TIMEFRAME_DEFAULT : i_stoTimeFrameTmp

i_stoSrc = close
i_stoLen = input.int(title=LBL_LENGTH, defval=14, minval=1, inline=LBL_LENGTH, group=stoGroupNameSettings)
i_stoOb = input.int(title=LBL_OVERBOUGHT, defval=70, minval=0, step=1, inline=LBL_LENGTH, group=stoGroupNameSettings)
i_stoOs = input.int(title=LBL_OVERSOLD, defval=30, minval=0, step=1, inline=LBL_LENGTH, group=stoGroupNameSettings, tooltip=TIP_OB_OS)

i_stoKSmooth = input.int(title=LBL_PERCK, defval=1, minval=1, maxval=10, inline=LBL_PERCK, group=stoGroupNameSettings)
i_stoDSmooth = input.int(title=LBL_PERCD, defval=3, minval=1, maxval=10, inline=LBL_PERCK, group=stoGroupNameSettings, tooltip=TIP_PK_PD)

var stoGroupNameWeight = 'SIGNAL WEIGHTS AND CONDITIONS'
i_stoSignalMode = input.string(title=LBL_SIGNAL_MODE, defval=ENTRIES, options=[ALL, ENTRIES_EXITS, ENTRIES, EXITS, ZONE], group=stoGroupNameWeight, tooltip=TIP_SIGNAL_MODE, inline=LBL_SIGNAL_MODE)
i_stoSignalPeriod = input.int(title=LBL_SIGNAL_PERIOD, defval=SIGNAL_PERIODS_DEFAULT, inline=LBL_SIGNAL_MODE, group=stoGroupNameWeight)
i_stoSignalCount = input.string(title=LBL_SIGNAL_COUNT, defval=SINGLE, options=[SINGLE, EVERY, THS, THE], inline=LBL_SIGNAL_MODE, group=stoGroupNameWeight, tooltip=TIP_SIGNAL_MODE)

i_stoES = input.int(title=LBL_ES, defval=ES_DEFAULT, minval=SIGNAL_MIN, maxval=SIGNAL_MAX, inline=LBL_ES, group=stoGroupNameWeight)
i_stoXL =  input.int(title=LBL_XL, defval=XL_DEFAULT, minval=SIGNAL_MIN, maxval=SIGNAL_MAX, inline=LBL_ES, group=stoGroupNameWeight)
i_stoSHORT_COLOR = input.color(title=LBL_SHORT_COLOR, defval=STO_COLOR, inline=LBL_ES, group=stoGroupNameWeight)

i_stoEL = input.int(title=LBL_EL, defval=EL_DEFAULT, minval=SIGNAL_MIN, maxval=SIGNAL_MAX, inline=LBL_EL, group=stoGroupNameWeight)
i_stoXS = input.int(title=LBL_XS, defval=XS_DEFAULT, minval=SIGNAL_MIN, maxval=SIGNAL_MAX, inline=LBL_EL, group=stoGroupNameWeight)
i_stoLONG_COLOR = input.color(title=LBL_LONG_COLOR, defval=STO_COLOR, inline=LBL_EL, group=stoGroupNameWeight)


// === FUNCTIONS ===

// â€”â€”â€”â€”â€” Function: Check is in condition zone mode
isZone(_sm) => _sm == ALL or _sm == ZONE

// â€”â€”â€”â€”â€” Function: Check is in condition start mode
isEntries(_sm) => _sm == ALL or _sm == ENTRIES

// â€”â€”â€”â€”â€” Function: Check is in condition end mode
isExits(_sm) => _sm == ALL or _sm == EXITS

// â€”â€”â€”â€”â€” Function: Check is in condition start mode
isEntriesAndExits(_sm) => _sm == ENTRIES_EXITS

// â€”â€”â€”â€”â€” Function: Get the weight based on the L, XL, S, XS conditions and add or not the condition weight x bars back (0 = current)   
getWeight(_series, _weight, _signalPeriod, _signalCount) =>
    seriesWeight = 0
    for i = 0 to _signalPeriod by 1
        if _series[i] == true
            seriesWeight += _weight
            if _signalCount == SINGLE
                break
    if (_signalCount == THS or _signalCount == THE)
        seriesWeight := (seriesWeight == (_weight * _signalPeriod)) ? _signalCount == THS ? _weight : seriesWeight : 0
    seriesWeight

// â€”â€”â€”â€”â€” Function: Multiply the value with given multiplier
applyMultiplier(_s, _m) => _s * _m

// â€”â€”â€”â€”â€” Function: Threshold the value with given threshold type and value
applyThreshold(_s, _t, _th) => _t == NO_THRESHOLD or _s >= _th ? _s : 0

// â€”â€”â€”â€”â€” Function: Limit the value with given limiter value
applyLimiter(_s, _l) => _s > _l ? _l : _s

// â€”â€”â€”â€”â€” Function: Get series where (up or under the candle) to draw the shape based on energy settings
getPlotSeries(_long, _weightLong, _short, _weightShort, _nrg) =>
    plotSeries = _long ? low - ta.tr * math.abs(_weightLong) * _nrg : _short ? high + ta.tr * math.abs(_weightShort) * _nrg : na
    plotSeries

// â€”â€”â€”â€”â€” Function: Get color based on brightness settings  
getPlotColor(_long, _weightLong, _colorLong, _short, _weightShort, _colorShort, _bright) =>
    plotColor = _long ? color.new(_colorLong, 100 - math.abs(_weightLong) / SIGNAL_MAX * 100 * _bright) : _short ? color.new(_colorShort, 100 - math.abs(_weightShort) / SIGNAL_MAX * 100 * _bright) : na
    plotColor

// â€”â€”â€”â€”â€” Function: Calculate STO
stoCalculate(_stoSrc, _stoLength, _stoOverbought, _stoOversold, _stoKSmooth, _stoDSmooth, _stoSignalMode) =>
    stoch = ta.stoch(_stoSrc, ta.highest(_stoLength), ta.lowest(_stoLength), _stoLength)
    stochK = ta.sma(stoch, _stoKSmooth)
    stochD = ta.sma(stochK, _stoDSmooth)

    stoOversold = ta.crossover(stochK, stochD) and stochD <= _stoOversold
    isstoOversoldZone = stoOversold and isZone(_stoSignalMode)
    isstoOversoldEntry = stoOversold[1] == false and stoOversold == true and (isEntries(_stoSignalMode) or isEntriesAndExits(_stoSignalMode))
    isstoOversoldExit = stoOversold[1] == true and stoOversold == false and (isExits(_stoSignalMode) or isEntriesAndExits(_stoSignalMode))

    stoOverbought = ta.crossunder(stochK, stochD) and stochD >= _stoOverbought
    isstoOverboughtZone = stoOverbought and isZone(_stoSignalMode)
    isstoOverboughtEntry = stoOverbought[1] == false and stoOverbought == true and (isEntries(_stoSignalMode) or isEntriesAndExits(_stoSignalMode))
    isstoOverboughtExit = stoOverbought[1] == true and stoOverbought == false and (isExits(_stoSignalMode) or isEntriesAndExits(_stoSignalMode))


    stoLongCalc = isstoOversoldZone or isstoOversoldEntry or isstoOversoldExit
    stoShortCalc = isstoOverboughtZone or isstoOverboughtEntry or isstoOverboughtExit
    [stoLongCalc, stoShortCalc]

bool stoLong = false
bool stoShort = false

[_stoLong, _stoShort] =  request.security(i_stoSymEnabled ? i_stoSymbol : syminfo.tickerid, i_stoTimeFrame, stoCalculate(i_stoSrc, i_stoLen, i_stoOb, i_stoOs, i_stoKSmooth, i_stoDSmooth, i_stoSignalMode))

if i_stoEnabled
    stoLong := _stoLong
    stoShort := _stoShort


// === CALCULATIONS ===

// â€”â€”â€”â€”â€” Calculations: Calculate L, XL, S and XS weight values based on trading conditions, input weight settings, how many x bars back and wheter to include the weight for each occurrence or a single occurence.   
signalEL = getWeight(stoLong, i_stoEL, i_stoSignalPeriod, i_stoSignalCount)
signalXL = getWeight(stoShort, i_stoXL, i_stoSignalPeriod, i_stoSignalCount)
signalES = getWeight(stoShort, i_stoES, i_stoSignalPeriod, i_stoSignalCount)
signalXS = getWeight(stoLong, i_stoXS, i_stoSignalPeriod, i_stoSignalCount) 

// â€”â€”â€”â€”â€” Calculations: Apply category weight settings - Multiplier
signalEL := applyMultiplier(signalEL, i_signalsMultiplier)
signalXL := applyMultiplier(signalXL, i_signalsMultiplier)
signalES := applyMultiplier(signalES, i_signalsMultiplier)
signalXS := applyMultiplier(signalXS, i_signalsMultiplier)

// â€”â€”â€”â€”â€” Calculations: Apply category weight settings - Threshold
signalEL := applyThreshold(signalEL, i_signalsThresholdType, i_signalsThreshold)
signalXL := applyThreshold(signalXL, i_signalsThresholdType, i_signalsThreshold)
signalES := applyThreshold(signalES, i_signalsThresholdType, i_signalsThreshold)
signalXS := applyThreshold(signalXS, i_signalsThresholdType, i_signalsThreshold)

// â€”â€”â€”â€”â€” Calculations: Apply category weight settings - Limiter
signalEL := applyLimiter(signalEL, i_signalsLimiter)
signalXL := applyLimiter(signalXL, i_signalsLimiter)
signalES := applyLimiter(signalES, i_signalsLimiter)
signalXS := applyLimiter(signalXS, i_signalsLimiter)

// â€”â€”â€”â€”â€” Calculations: Extract passthrough EL, XL, ES and XS signal values from connector
[ptSignalEL, ptSignalXL, ptSignalES, ptSignalXS] = azconnector.extract(i_connector)

// â€”â€”â€”â€”â€” Calculations: Combine output L, XL, S and XS weight values to single number based on input settings
connectorOut = switch i_connectorMode
    BOTH => azconnector.compose(signalEL + ptSignalEL, signalXL + ptSignalXL, signalES + ptSignalES, signalXS + ptSignalXS)
    INDICATOR => azconnector.compose(signalEL, signalXL, signalES, signalXS)
    CONNECTOR => azconnector.compose(ptSignalEL, ptSignalXL, ptSignalES, ptSignalXS)


// == VISUALS ===

// â€”â€”â€”â€”â€” Plot Category Symbol
var indicatorSymbol = CAT_ICON
indicatorPlotSeries = getPlotSeries(signalEL >= i_signalsThreshold, signalEL, signalES >= i_signalsThreshold, signalES, i_indicatorEnergy)
indicatorPlotColor = getPlotColor(signalEL >= i_signalsThreshold, signalEL, LONG_COLOR, signalES >= i_signalsThreshold, signalES, SHORT_COLOR, i_indicatorBrightness)
plotshape(series=indicatorPlotSeries, style=indicatorSymbol, title='Settings Indicator', location=location.absolute, color=indicatorPlotColor, size=size.small)

// â€”â€”â€”â€”â€” Plot Indicator Symbol
stoPlotSeries = i_stoEnabled ? getPlotSeries(stoLong, i_stoEL == 0 ? i_stoXS : i_stoEL, stoShort, i_stoES == 0 ? i_stoXL : i_stoES, i_indicatorEnergy) : na
stoPlotColor = i_stoEnabled ? getPlotColor(stoLong,  i_stoEL == 0 ? i_stoXS : i_stoEL, i_stoLONG_COLOR, stoShort,  i_stoES == 0 ? i_stoXL : i_stoES, i_stoSHORT_COLOR, i_indicatorBrightness) : na
plotshape(series=stoPlotSeries, title=stoGroupNameSettings, style=STO_ICON, location=location.absolute, color=stoPlotColor, size=size.tiny)

// â€”â€”â€”â€”â€” Plot Strategy Connector
plot(connectorOut, title='Signal connector', display=display.none)